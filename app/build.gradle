apply plugin: 'com.android.application'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "me.iacn.biliroaming"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 4
        versionName "1.0.6"
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == 'release') {
                outputFileName = "BiliRoaming_${defaultConfig.versionName}.apk"
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'
}

afterEvaluate {
    installDebug.doLast {
        forceStopBiliBili.execute()
        startBiliBili.execute()
    }
}

task forceStopBiliBili(type: Exec) {
    commandLine android.adbExecutable, 'shell', 'am', 'force-stop', 'tv.danmaku.bili'
}

task startBiliBili(type: Exec) {
    commandLine android.adbExecutable, 'shell', 'monkey', '-p', 'tv.danmaku.bili', '1'
}